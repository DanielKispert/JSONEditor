plugins {
    id 'application'
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '2.0.1'
}

def versionProps = new Properties()
file("src/main/resources/version.properties").withInputStream { versionProps.load(it) }
version = versionProps.getProperty('version')

repositories {
    mavenCentral()
    flatDir {
        dirs 'directLibs'
    }
}

application {
    mainClass.set('com.daniel.jsoneditor.Main')
}

jar {
    manifest {
        attributes 'Main-Class': 'com.daniel.jsoneditor.Main'
    }
}

javafx {
    version = '21.0.1'
    modules = [ 'javafx.controls' ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}


dependencies {
    implementation 'io.github.copilot-community-sdk:copilot-sdk:1.0.5'
    implementation 'com.networknt:json-schema-validator:1.0.87'
    implementation 'ch.qos.logback:logback-classic:1.5.17'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r'
    implementation 'com.brunomnsilva:smartgraph:2.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.testfx:testfx-core:4.0.18'
    testImplementation 'org.testfx:testfx-junit5:4.0.18'
    testRuntimeOnly 'org.testfx:openjfx-monocle:jdk-12.0.1+2'

    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

test {
    useJUnitPlatform()
}

runtime {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']
    additive = true
    modules = ['jdk.unsupported']

    jpackage {
        imageName = 'JSONEditor'
        // macOS jpackage requires first version number > 0, so increment by 1: 0.16.0 -> 1.16.0
        def versionParts = version.toString().split('\\.')
        def majorVersion = versionParts[0] as int
        if (majorVersion == 0) {
            appVersion = "1.${versionParts[1]}.${versionParts[2]}"
        } else {
            appVersion = version
        }
        skipInstaller = true
    }
}
